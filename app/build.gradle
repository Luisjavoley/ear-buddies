apply plugin: 'com.android.application'
apply plugin: 'jacoco'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.1"

    defaultConfig {
        applicationId "ch.epfl.sweng.project"
        minSdkVersion 15
        targetSdkVersion 25

        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables {
            useSupportLibrary = true
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }

    //noinspection GroovyMissingReturnStatement
    lintOptions {
        // Disable lint warnings about app indexing, and about non-latest tools versions
        // TODO consider re-enabling app indexing before publishing your application.
        disable 'GoogleAppIndexingWarning', 'GradleDependency'
        // Jackson make a lint error, we avoid it by adding warning instead of error
        warning 'InvalidPackage'
        abortOnError false
    }

    // packages create a conflict with spring
    //noinspection GroovyMissingReturnStatement
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }

    // Workaround for an Android Studio bug in the "Missing Return Statement" warning
    return void
}

dependencies {
    // Chrome custom tabs, used to access lastfm website when the user click on covers
    compile 'com.android.support:customtabs:25.0.1'
    compile 'com.android.support:appcompat-v7:25.0.1'
    compile 'com.android.support:design:25.0.1'
    // Unit testing dependencies
    testCompile 'junit:junit:4.12'
    // Mockito framework
    testCompile 'org.mockito:mockito-core:1.10.19'
    // Instrumented tests
    androidTestCompile 'com.android.support:support-annotations:25.0.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    // Optional -- Hamcrest library
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    // Optional -- UI testing with Espresso
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    // Support for RecyclerView actions testing with Espresso
    // Currently having problems with SDK higher than 23.0.1 ??
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.2') {
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'support-v4'
    }

    // Spring import for restful api
    compile 'org.springframework.android:spring-android-rest-template:2.0.0.M3'

    // JacksonMapper
    //noinspection InvalidPackage
    compile 'com.fasterxml.jackson.core:jackson-core:2.8.5'
    //noinspection InvalidPackage
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.8.5'
    //noinspection InvalidPackage
    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.5'

    // facebook dependencies
    compile 'com.facebook.android:facebook-android-sdk:[4,5)'
    compile 'com.google.android.gms:play-services-appindexing:9.8.0'


    //Map and localisation
    compile 'com.google.android.gms:play-services-maps:9.8.0'
    compile 'com.google.android.gms:play-services-location:9.8.0'
}

// Spring import.
repositories {
    maven {
        url 'https://repo.spring.io/libs-milestone'
    }
}
// Do not change the below code unless you're sure you know what you're doing
// Also make sure Jenkins config is synced with this one.

jacoco {
    toolVersion = "0.7.7.201606060606"
}

task jacocoReport(type: JacocoReport, dependsOn: "connectedAndroidTest") {
    sourceDirectories = files(['**/src/main/java'])

    classDirectories = fileTree(
            dir: '**/classes',
            excludes: [
                    '**/R.class',
                    '**/R$*.class',
                    '**/BuildConfig.*',
                    '**/Manifest*.*',
                    '**/*Test*.*',
                    '**/android/**/*.*',
                    '**/CustomTabActivityHelper.class',
                    '**/CustomTabsHelper.class',
                    '**/KeepAliveService.class',
                    '**/ServiceConnection.class',
                    '**/ServiceConnectionCallback.class',
                    '**/WebviewFallBack.class',

            ]
    )

    executionData = files('build/outputs/code-coverage/connected/coverage.ec')
}